apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.android.application'

buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.31.2'
    }
}

repositories {
    maven { url 'https://maven.fabric.io/public' }
}

ext {
    developerId = 'drxeno02'
    developerName = 'Leonard Tatum'
    developerEmail = 'ljtatum@hotmail.com'

    siteUrl = 'https://github.com/drxeno02/tools-tip-calculator'
    gitUrl = 'https://github.com/drxeno02/tools-tip-calculator.git'
}

android {
    compileSdkVersion 29

    // construct version name
    def versionMajor = 2
    def versionMinor = 0
    def versionPatch = 13
    def code = 14;

    defaultConfig {
        applicationId "com.blog.ljtatum.tipcalculator"
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
        minSdkVersion 19
        targetSdkVersion 29
        versionCode code

        // multidex
        multiDexEnabled true
        //noinspection GroovyAssignabilityCheck
        signingConfig signingConfigs.debug
    }
    // specifies a flavor dimension
    flavorDimensions "app"
    buildTypes {
        debug {
            applicationIdSuffix '.debug'
            buildConfigField "String", "ENVIRONMENT", "\"test\""
            buildConfigField "boolean", "DEBUG_MODE", "true"
            manifestPlaceholders = [build_version: "${versionMajor}.${versionMinor}.${versionPatch}"]

            // debuggable
            debuggable true
        }
        release {
            buildConfigField "String", "ENVIRONMENT", "\"production\""
            buildConfigField "boolean", "DEBUG_MODE", "false"
            manifestPlaceholders = [build_version: "${versionMajor}.${versionMinor}.${versionPatch}"]

            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            proguardFile 'proguard-rules.pro'
        }
    }
    // flavor implementation propelled by request for multiple environments on Crashlytics
    productFlavors {
        standard {
            applicationId 'com.blog.ljtatum.tipcalculator'
            dimension "app"
        }
    }
    lintOptions {
        // if true, stop the gradle build if errors are found
        checkReleaseBuilds false
        abortOnError false
    }
}

dependencies {
    // application framework
    api project(path: ':ApplicationFramework')
}