buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.+'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

repositories {
    maven { url 'https://maven.fabric.io/public' }
}

ext {
    developerId = 'drxeno02'
    developerName = 'Leonard Tatum'
    developerEmail = 'ljtatum@hotmail.com'

    siteUrl = 'https://github.com/drxeno02/tools-tip-calculator'
    gitUrl = 'https://github.com/drxeno02/tools-tip-calculator.git'
}

android {
    compileSdkVersion 26

    // construct version name
    def versionMajor = 2
    def versionMinor = 0
    def versionPatch = 11
    def code = 12;

    defaultConfig {
        applicationId "com.blog.ljtatum.tipcalculator"
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
        minSdkVersion 16
        targetSdkVersion 26
        versionCode code

        //noinspection GroovyAssignabilityCheck
        signingConfig signingConfigs.debug
    }
    // specifies a flavor dimension
    flavorDimensions "app"
    buildTypes {
        debug {
            applicationIdSuffix '.debug'
            buildConfigField "String", "ENVIRONMENT", "\"test\""
            buildConfigField "boolean", "DEBUG_MODE", "true"
            manifestPlaceholders = [build_version: "${versionMajor}.${versionMinor}.${versionPatch}"]

            // debuggable
            debuggable true
        }
        release {
            buildConfigField "String", "ENVIRONMENT", "\"production\""
            buildConfigField "boolean", "DEBUG_MODE", "false"
            manifestPlaceholders = [build_version: "${versionMajor}.${versionMinor}.${versionPatch}"]

            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            proguardFile 'proguard-rules.pro'
        }
    }
    // flavor implementation propelled by request for multiple environments on Crashlytics
    productFlavors {
        standard {
            applicationId 'com.blog.ljtatum.tipcalculator'
            dimension "app"
        }
    }
    lintOptions {
        // if true, stop the gradle build if errors are found
        abortOnError false
    }
}

dependencies {
    api project(path: ':ApplicationFramework')
    api('com.crashlytics.sdk.android:crashlytics:2.6.7@aar') {
        transitive = true;
    }
}

// THIS IS REQUIRED FOR FIREBASE (KEEP HERE)
apply plugin: 'com.google.gms.google-services'
